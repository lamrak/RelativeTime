plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group 'ca.nihk'
version '0.0.6'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xuse-experimental=kotlin.time.ExperimentalTime"
        ]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xuse-experimental=kotlin.time.ExperimentalTime"
        ]
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "nihk"
            name "nihk"
            email "nrose2@alumni.uwo.ca"
        }
    }

    scm {
        url "https://github.com/nihk/RelativeTime"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'ca.nihk'
            artifactId 'relativetime'
            version '0.0.6'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A library that helps you define relative time, e.g. "5 hours ago" or "1 month from now"')
                root.appendNode('name', 'Relative Time')
                root.appendNode('url', 'https://github.com/nihk/RelativeTime')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'relativetime'
        name = 'relativetime'
        userOrg = 'nickjrose'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nihk/RelativeTime.git'
        version {
            name = '0.0.6'
            desc = '0.0.6'
            released  = new Date()
        }
    }
}